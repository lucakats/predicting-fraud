/* ----- Part 1 ----- */

PROC IMPORT FILE = '/home/u61459923/Assignment 2/Fraud.csv'
OUT = FRAUD DBMS = CSV REPLACE;
RUN;

DATA FRAUD; SET FRAUD;
MISS = NMISS(OF ACT--PRCC_F);
RUN;

PROC SORT DATA = FRAUD; BY GVKEY FYEAR MISS; RUN;

DATA FRAUD; SET FRAUD;
BY GVKEY FYEAR MISS;
IF FIRST.FYEAR;
GVKEY1 = LAG(GVKEY);
FYEAR1 = LAG(FYEAR);
AT1 = LAG(AT);
ATA = (AT+AT1)/2;

*RSST Accruals;
WC = (ACT-CHE) - (LCT-DLC);
WC1 = LAG(WC);
CH_WC = WC-WC1;
NCO = (AT-ACT-IVAO) - (LT-LCT-DLTT);
NCO1 = LAG(NCO);
CH_NCO = NCO-NCO1;
FIN = (IVST+IVAO) - (DLTT+DLC+PSTK);
FIN1 = LAG(FIN);
CH_FIN = FIN-FIN1;
IF GVKEY = GVKEY1 AND FYEAR = FYEAR1+1 THEN DO;
RSST_ACC = (CH_WC+CH_NCO+CH_FIN)/ATA;
END;

*Change in Receivables;
RECT1 = LAG(RECT);
CH_RECT = RECT-RECT1;
IF GVKEY = GVKEY1 AND FYEAR = FYEAR1+1 THEN DO;
CH_REC = CH_RECT/ATA;
END;

*Change in Inventory;
INVT1 = LAG(INVT);
CH_INVT = INVT-INVT1;
IF GVKEY = GVKEY1 AND FYEAR = FYEAR1+1 THEN DO;
CH_INV = CH_INVT/ATA;
END;

*% Soft Assets;
SOFT_ASSETS = (AT-PPENT-CHE)/AT;

*Change in Cash Sales (percentage);
CS = SALE-CH_RECT;
CS1 = LAG(CS);
IF GVKEY = GVKEY1 AND FYEAR = FYEAR1+1 THEN DO;
CH_CS = CS/CS1*100;
END;

*Change in ROA;
IB1 = LAG(IB);
ATA1 = LAG(ATA);
IF GVKEY = GVKEY1 AND FYEAR = FYEAR1+1 THEN DO;
CH_ROA = (IB/ATA)-(IB1/ATA1);
END;

*Actual issuance;
IF SSTK GT 0 THEN ISSUE = 1;
ELSE IF DLTIS GT 0 THEN ISSUE = 1;
ELSE ISSUE = 0;

ARRAY W(6) RSST_ACC CH_REC CH_INV SOFT_ASSETS CH_CS CH_ROA;
DO I = 1 TO 6;
IF W(I) = . THEN DELETE;
END;
RUN;

PROC SORT DATA = FRAUD; BY FYEAR; RUN;

PROC MEANS DATA = FRAUD NOPRINT; BY FYEAR;
VAR RSST_ACC CH_REC CH_INV SOFT_ASSETS CH_CS CH_ROA;
OUTPUT OUT = WINS (DROP=_TYPE_ _FREQ_) P1 = L1-L6 P99 = H1-H6;
RUN;

DATA FRAUD; MERGE FRAUD WINS; BY FYEAR;
ARRAY V(*) RSST_ACC CH_REC CH_INV SOFT_ASSETS CH_CS CH_ROA;
ARRAY H(*) H1-H6;
ARRAY L(*) L1-L6;
DO I = 1 TO 6;
IF V(I) < L(I) THEN V(I) = L(I);
IF V(I) > H(I) THEN V(I) = H(I);
END;
KEEP GVKEY FYEAR FRAUD RSST_ACC CH_REC
CH_INV SOFT_ASSETS CH_CS CH_ROA ISSUE;
RUN;

DATA FRAUD0 FRAUD1; SET FRAUD;
IF FRAUD = 0 THEN OUTPUT FRAUD0;
ELSE OUTPUT FRAUD1;
RUN;

PROC SQL;
CREATE TABLE FRAUD_N AS
	SELECT COUNT(*) AS FRAUD_N
	FROM FRAUD1;
QUIT;

PROC SURVEYSELECT DATA = FRAUD0 OUT = FRAUD0
METHOD = SRS
SAMPSIZE = 20000
SEED = 555
NOPRINT;
RUN;

DATA ASS2.FRAUD; SET FRAUD0 FRAUD1;
IF _N_ = 1 THEN DO;
SET FRAUD_N;
END;
RUN;

PROC SORT DATA = ASS2.FRAUD; BY GVKEY FYEAR; RUN;

/* ----- Part 2 ----- */

DATA ASS2.FRAUD; SET ASS2.FRAUD;
LABEL
GVKEY = "Company Identifier"
FYEAR = "Fiscal Year of Entry"
FRAUD = "Fraud (Fraudulent = 1)"
RSST_ACC = "RSST Accruals"
CH_REC = "Change in Receivables"
CH_INV = "Change in Inventory"
SOFT_ASSETS = "% Soft Assets"
CH_CS = "% Change in Cash Sales"
CH_ROA = "Change in Return on Assets"
ISSUE = "Issuance (Issued Securities = 1)"
FRAUD_N = "Total No. of Fraudulent Cases";
RUN;

PROC CONTENTS DATA = ASS2.FRAUD; RUN;

PROC MEANS DATA = ASS2.FRAUD MEDIAN MEAN RANGE STDDEV MAXDEC = 3;
TITLE 'Descriptive Statistics of Continuous Variables';
VAR RSST_ACC CH_REC CH_INV SOFT_ASSETS CH_CS CH_ROA;
RUN;

PROC SQL;
TITLE 'Fraud Split in Sample';
SELECT FRAUD, COUNT(*) AS COUNT
FROM ASS2.FRAUD
GROUP BY FRAUD;
QUIT;

PROC FREQ DATA = ASS2.FRAUD NOPRINT;
TABLES FRAUD * FYEAR / OUT = FRAUDYEAR;
RUN;

PROC SGPLOT DATA = FRAUDYEAR;
TITLE 'Fraud Count per Year';
WHERE FRAUD = 1;
VBARBASIC FYEAR / RESPONSE = COUNT DATALABEL BARWIDTH = 0.9;
XAXIS LABEL = 'Year';
YAXIS LABEL = 'Fraud Count'; 
RUN;

PROC SQL;
TITLE 'Unique Company Keys';
SELECT COUNT(DISTINCT GVKEY) AS UNIQUE_GVKEYS FROM ASS2.FRAUD;
QUIT;

PROC FREQ DATA = ASS2.FRAUD ORDER = FREQ NOPRINT;
TABLES GVKEY / OUT = GVKEY;
RUN;

PROC FREQ DATA = GVKEY ORDER = FREQ NOPRINT;
TABLES COUNT / OUT = GVKEY2;
RUN;

DATA GVKEY2; SET GVKEY2;
PERCENT = ROUND(PERCENT, 0.01);
RUN;

PROC SGPLOT DATA = GVKEY2;
VBARBASIC COUNT / RESPONSE = PERCENT DATALABEL BARWIDTH = 0.9;
XAXIS LABEL = 'Incidences';
YAXIS LABEL = 'Frequency (Percentage %)'; 
TITLE 'GVKEY Incidence Frequency';
RUN;

PROC FREQ DATA = ASS2.FRAUD NOPRINT;
TABLES FYEAR / OUT = FYEAR;
RUN;

DATA FYEAR; SET FYEAR;
PERCENT = ROUND(PERCENT, 0.01);
RUN;

PROC SGPLOT DATA = FYEAR;
VBARBASIC FYEAR / RESPONSE = PERCENT DATALABEL BARWIDTH = 0.9;
XAXIS LABEL = 'Year';
YAXIS LABEL = 'Frequency (Percentage %)'; 
TITLE 'FYEAR Frequency';
RUN;

PROC FREQ DATA = ASS2.FRAUD;
TABLES ISSUE * FRAUD / NOPERCENT NOROW;
TITLE 'Issuance Split in Sample';
RUN;

ODS OUTPUT EQUALITY = EQ TTESTS = TTT;
PROC TTEST DATA = ASS2.FRAUD;
CLASS FRAUD;
VAR RSST_ACC CH_REC CH_INV SOFT_ASSETS CH_CS CH_ROA ISSUE;
RUN;
ODS OUTPUT CLOSE;

PROC SORT DATA = TTT; BY VARIABLE; RUN;
PROC SORT DATA = EQ; BY VARIABLE; RUN;
DATA EQ; SET EQ;
RENAME METHOD = METHOD1;
RUN;

DATA TT; MERGE TTT EQ; BY VARIABLE; RUN;

DATA TT; SET TT;
IF PROBF LT 0.05 AND VARIANCES EQ "Unequal" THEN OUTPUT;
IF PROBF GE 0.05 AND VARIANCES EQ "Equal" THEN OUTPUT;
DROP DF NUMDF DENDF TVALUE FVALUE METHOD1;
RUN;

PROC SORT DATA = TT; BY DESCENDING VARIANCES PROBT; RUN;

PROC PRINT DATA = TT; RUN;








/* ----- Part 3 ----- */

ODS OUTPUT ParameterEstimates = PMOUT;
PROC LOGISTIC DATA = ASS2.FRAUD OUTEST = FRAUDEST;
MODEL FRAUD (EVENT = "1") = RSST_ACC CH_REC CH_INV SOFT_ASSETS CH_CS CH_ROA ISSUE;
RUN;
ODS OUTPUT CLOSE;

DATA PMOUT; SET PMOUT;
DROP DF STDERR _ESTTYPE_;
RETAIN ORDER;
IF _N_ = 1 THEN DO;
ORDER = 0;
END;
ORDER = ORDER + 1;
RUN;

DATA COMPARE;
INPUT VARIABLE $12. ESTIMATE WALDCHISQ PROBCHISQ ORDER;
CARDS;
Intercept*   -7.893 1180.0 0.001 1
RSST_ACC*    0.790  24.1   0.001 2
CH_REC*      2.518  28.5   0.001 3
CH_INV*      1.191  4.4    0.019 4
SOFT_ASSETS* 1.979  86.3   0.001 5
CH_CS*       0.171  17.0   0.001 6
CH_ROA*      -0.932 19.9   0.001 7
ISSUE*	     1.029  28.5   0.001 8
;
RUN;

PROC APPEND BASE = COMPARE DATA = PMOUT FORCE; RUN;

PROC SORT DATA = COMPARE; BY ORDER VARIABLE; RUN;

DATA COMPARE; SET COMPARE (DROP = ORDER); RUN;

PROC PRINT DATA = COMPARE; RUN;

DATA FRAUDEST; SET FRAUDEST (KEEP = INTERCEPT--ISSUE);
RENAME RSST_ACC = CO_RSST_ACC CH_REC = CO_CH_REC CH_INV = CO_CH_INV
SOFT_ASSETS = CO_SOFT_ASSETS CH_CS = CO_CH_CS CH_ROA = CO_CH_ROA ISSUE = CO_ISSUE;
RUN;

DATA FSCORE1; SET ASS2.FRAUD;
IF _N_ EQ 1 THEN DO;
SET FRAUDEST;
END;
PRED = INTERCEPT + (RSST_ACC*CO_RSST_ACC) + (CH_REC*CO_CH_REC) + (CH_INV*CO_CH_INV) + 
(SOFT_ASSETS*CO_SOFT_ASSETS) + (CH_CS*CO_CH_CS) + (CH_ROA*CO_CH_ROA) + (ISSUE*CO_ISSUE);
PROB = EXP(PRED)/(1+EXP(PRED));
UNC_PROB = FRAUD_N/(FRAUD_N+20000);
FSCORE = PROB/UNC_PROB;
KEEP FRAUD FSCORE FRAUD_N;
RUN;

PROC RANK DATA = FSCORE1 OUT = RESULTS GROUPS = 5;
VAR FSCORE;
RANKS RANK;
RUN;

DATA RESULTS; SET RESULTS;
RANK = RANK+1;
RUN;

PROC SORT DATA = RESULTS OUT = FSCORE2; BY RANK FRAUD; RUN;

PROC MEANS DATA = FSCORE2 NOPRINT;
VAR FSCORE;
OUTPUT OUT = FSCORE MIN = MIN_FSCORE;
BY RANK FRAUD;
RUN;

DATA FSCORE; SET FSCORE (DROP = _TYPE_);
IF _N_ = 1 THEN DO;
SET FRAUD_N;
END;
FORMAT PCT_OF_TOTAL PERCENT12.2;
RENAME _FREQ_ = N;
IF FRAUD = 0 THEN PCT_OF_TOTAL = _FREQ_/20000;
ELSE PCT_OF_TOTAL = _FREQ_/FRAUD_N;
DROP FRAUD_N;
RUN;

PROC PRINT DATA = FSCORE; RUN;

DATA FSCORE1; SET FSCORE (DROP = N PCT_OF_TOTAL); RUN;

DATA FSCORE0; SET FSCORE1;
IF FRAUD EQ 0;
DROP FRAUD;
RENAME MIN_FSCORE = MIN_FSCORE0;
RUN;

DATA FSCORE1; SET FSCORE1;
IF FRAUD EQ 1;
DROP FRAUD;
RENAME MIN_FSCORE = MIN_FSCORE1;
RUN;

PROC SORT DATA = FSCORE0; BY RANK; RUN;
PROC SORT DATA = FSCORE1; BY RANK; RUN;

DATA FDIFF; MERGE FSCORE0 FSCORE1; BY RANK;
MIN_FSCORE_DIFF = MIN_FSCORE1-MIN_FSCORE0;
RUN;

PROC SGPLOT DATA = FDIFF;
STYLEATTRS DATASYMBOLS = (CircleFilled)
DATACONTRASTCOLORS = (NAVY MAGENTA RED);
SCATTER X = RANK Y = MIN_FSCORE0;
SCATTER X = RANK Y = MIN_FSCORE1;
SCATTER X = RANK Y = MIN_FSCORE_DIFF / DATALABEL;
YAXIS LABEL = 'Minimum F-Score';
XAXIS LABEL = 'Quintile Rank';
RUN;

PROC SGPLOT DATA = FSCORE;
SERIES X = RANK Y = PCT_OF_TOTAL / GROUP = FRAUD LINEATTRS = (THICKNESS = 4);
XAXIS LABEL = 'F-Score Quintile';
YAXIS LABEL = '% Of Total';	
RUN;

PROC LOGISTIC DATA = ASS2.FRAUD;
MODEL FRAUD (EVENT = "1") = RSST_ACC CH_REC CH_INV SOFT_ASSETS CH_CS CH_ROA ISSUE /
OUTROC = ROC CTABLE PPROB=(0 TO 0.15 BY 0.005);
RUN;












/* ----- Part 4 ----- */

PROC LOGISTIC DATA = ASS2.FRAUD;
MODEL FRAUD (EVENT = "1") = RSST_ACC CH_REC CH_INV SOFT_ASSETS CH_CS CH_ROA ISSUE /
SELECTION = STEPWISE;
RUN;

PROC SORT DATA = ASS2.FRAUD; BY FYEAR; RUN;

%MACRO BACKTEST(N);
	%DO I = 1 %TO &N.;
	
		DATA BTEST; SET ASS2.FRAUD;
		BY FYEAR;
		RETAIN RANDOM;
		IF FIRST.FYEAR THEN DO;
			RANDOM = RAND('UNIFORM');
		END;
		RUN;
		
		PROC SORT DATA = BTEST; BY RANDOM; RUN;
		
		DATA EST TEST; SET BTEST;
		BY RANDOM;
		IF _N_ = 1 THEN N = 0;
		RETAIN N;
		IF FIRST.RANDOM THEN N = N+1;
		IF N LE 10 THEN OUTPUT EST;
		ELSE OUTPUT TEST;
		RUN;
		
		PROC LOGISTIC DATA = EST NOPRINT OUTEST = BTEST_EST;
		MODEL FRAUD (EVENT = "1") = RSST_ACC SOFT_ASSETS ISSUE;
		RUN;
		
		DATA BTEST_EST; SET BTEST_EST (KEEP = INTERCEPT--ISSUE);
		RENAME
		RSST_ACC = CO_RSST_ACC
		SOFT_ASSETS = CO_SOFT_ASSETS
		ISSUE = CO_ISSUE;
		RUN;
		
		PROC SQL;
		CREATE TABLE FRAUD_N1 AS
		SELECT FRAUD, COUNT(*) AS FRAUD_N1
		FROM TEST
		GROUP BY FRAUD;
		CREATE TABLE TOTAL_N AS
		SELECT COUNT(*) AS TOTAL_N
		FROM TEST;
		QUIT;
		
		DATA FRAUD_N1; SET FRAUD_N1;
		IF FRAUD EQ 0 THEN DELETE;
		DROP FRAUD;
		RUN;
		
		DATA TEST; SET TEST;
		IF _N_ EQ 1 THEN DO;
		SET FRAUD_N1;
		SET TOTAL_N;
		END;
		RUN;

		DATA BTEST_F; SET TEST;
		IF _N_ EQ 1 THEN DO;
		SET BTEST_EST;
		END;
		PRED = INTERCEPT + (RSST_ACC*CO_RSST_ACC) + (SOFT_ASSETS*CO_SOFT_ASSETS) + 
		(ISSUE*CO_ISSUE);
		PROB = EXP(PRED)/(1+EXP(PRED));
		UNC_PROB = FRAUD_N1/TOTAL_N;
		FSCORE = PROB/UNC_PROB;
		KEEP FRAUD FSCORE;
		RUN;
		
		PROC SORT DATA = BTEST_F; BY FRAUD; RUN;
		
		PROC MEANS DATA = BTEST_F NOPRINT;
		VAR FSCORE;
		BY FRAUD;
		OUTPUT OUT = STATS MEAN = MEAN MIN = MIN Q1 = Q1
		MEDIAN = MEDIAN Q3 = Q3 MAX = MAX;
		RUN;
		
		DATA STATS; SET STATS;
		KEEP FRAUD MEAN MIN Q1 MEDIAN Q3 MAX;
		RUN;
		
		%IF &I. EQ 1 %THEN %DO;
			DATA FSCORESTATS; SET STATS; RUN;
		%END;
		%ELSE %DO;
			PROC APPEND BASE = FSCORESTATS DATA = STATS; RUN;
		%END;
		
	%END;
%MEND;

%BACKTEST(100);

PROC MEANS DATA = FSCORESTATS MEAN MEDIAN;
CLASS FRAUD;
RUN;

PROC TTEST DATA = FSCORESTATS;
CLASS FRAUD;
VAR MEAN;
RUN;